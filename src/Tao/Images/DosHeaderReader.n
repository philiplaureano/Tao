using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Tao.Interfaces;

using System;
using System.Collections.Generic;
using System.IO;
using System.Linq;

namespace Tao
{
  /// <summary>
  /// Represents a class that is responsible for reading the legacy MS-DOS header of a portable executable file.
  /// </summary>
  public class DosHeaderReader : IDosHeaderReader
  {    
    public Read([NotNull] inputStream : Stream) : IDosHeader
        requires inputStream.Length > 128 otherwise throw BadImageFormatException("Invalid MS-DOS Header")
        requires inputStream.Position == 0
        ensures value != null && value.Data.Length > 0
    {
      def binaryReader = BinaryReader(inputStream);
      def bytesToRead = 128;        
      def bytes = array(bytesToRead) : array[byte];
      
      _ = binaryReader.Read(bytes, 0, bytesToRead);
      
      match(bytes[0], bytes[1])
      {
          // Check for the 'MZ' string
        | (0x4d : byte, 0x5a : byte) => DosHeader(MemoryStream(bytes));
        | _ =>  throw BadImageFormatException("Invalid MS-DOS Header");
      }            
    }
  }
}

using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.IO;
using System.Collections.Generic;
using System.Linq;

using Tao.Interfaces;
namespace Tao
{
    public class SectionWriter : ISectionWriter, IImageWriter
    {
        public Write([NotNull] sections : IEnumerable[ISection], sectionAlignment : uint, outputStream : Stream) : void
        {
            def writer = BinaryWriter(outputStream);
            foreach(section in sections)
            {
                def sectionData = section.Data;
                _ = sectionData.Seek(0, SeekOrigin.Begin);
                
                def header = section.Header;
                def reader = BinaryReader(sectionData);

                def length = header.SizeOfRawData;                    
                def sectionBytes = reader.ReadBytes(length :> int);

                def startPosition = header.PointerToRawData;
                _ = outputStream.Seek(startPosition, SeekOrigin.Begin);
                writer.Write(sectionBytes);
                
                // Align the sections to the nearest boundary
                outputStream.Align(sectionAlignment :> int);
            }
        }
        public Write([NotNull] image : IImage, [NotNull] outputStream : Stream) : uint
            requires image.OptionalHeader != null && image.OptionalHeader.NTHeader != null
            requires image.Sections != null
        {
            def startPosition = outputStream.Position :> uint;
            def optionalHeader = image.OptionalHeader;
            def ntHeader = optionalHeader.NTHeader;
            
            def sectionAlignment = ntHeader.SectionAlignment;
            Write(image.Sections, sectionAlignment, outputStream);
            
            startPosition;
        }
    }
}

using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Tao.Interfaces;

using System;
using System.Collections.Generic;
using System.Linq;

namespace Tao
{
  public class SectionResolver : ISectionResolver
  {
    public Resolve([NotNull] sections : IEnumerable.[ISectionHeader], rva : uint) : ISectionHeader
    {
      def matchesRva(header : ISectionHeader, currentRva : uint) : bool
      {
        def startAddress = header.VirtualAddress;
        def boundary = startAddress + header.SizeOfRawData;
          
        currentRva >= startAddress && currentRva <= boundary;
      }
      
      def matchesGivenRva = matchesRva(_, rva);
      def result = sections.FirstOrDefault(matchesGivenRva);
      
      when(result == null)
      {
        def message = string.Format("The Rva '{0}' doesn't correspond to any of the given sections", rva);
        throw ArgumentException(message);
      }
      
      result;
    }
  }
}
